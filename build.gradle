buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
    }
}


allprojects {
    group = 'info.developerblog.spring.thrift'
    version = '1.4.0.RELEASE'

    repositories {
        jcenter()
    }

    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'provided-base'
    apply plugin: 'optional-base'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Camden.SR3"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'org.codehaus.groovy:groovy-all:2.4.5'
            dependency 'org.apache.thrift:libthrift:0.9.3'
            dependency 'org.apache.commons:commons-pool2:2.4.2'
            dependency 'org.apache.commons:commons-lang3:3.5'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        provided "org.projectlombok:lombok"
        compile("org.springframework.boot:spring-boot-starter-web:1.5.1.RELEASE")

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}
